#!/usr/bin/perl

# Copyright (c) 2022 Todd T. Fries <todd@fries.net>
#
# Permission to use, copy, modify, and distribute this software for any
# purpose with or without fee is hereby granted, provided that the above
# copyright notice and this permission notice appear in all copies.
#
# THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
# WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
# MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
# ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
# WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
# ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
# OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.

use strict;
use warnings;

use DBD::Pg qw(:pg_types);
use DBI qw(:sql_types);
use Date::Manip;
use FDC::db;
use Getopt::Std;
use POSIX qw(getpid);
use ReadConf;
use Term::ReadLine;

our $opt_v; # verbose
our $opt_c; # config file
our $opt_p; # poll on/off

$opt_c = $ENV{'HOME'}."/.config/dbq/dbq.conf";

getopts('c:pv:');

our $v = $opt_v;

if (!defined($v)) {
	$v = 0;
}
if (! -f $opt_c) {
	die "conf file '${opt_c}' EEXIST";
}

my $dbinfo = {
	dsn => "",
	user => "",
	pass => "",
};

my $dbprep = { };

my $memcache = { };

my $r = ReadConf->new;
our $conf = $r->readconf($opt_c);
my $config = $conf->{_};
if (defined($config)) {
	foreach my $id (keys %{$dbinfo}) {
		if (defined($config->{$id})) {
			$dbinfo->{$id} = $config->{$id};
		}
	}
}

foreach my $id (keys %{$dbinfo}) {
	printf "dbinfo=>{%s} = '%s'\n", $id, $dbinfo->{$id};
}

our $db;

init_db($dbinfo);

if (defined($opt_p)) {
	polldb();
	# notreached
}

our $term = Term::ReadLine->new('dbq', *STDIN, *STDOUT);
$term->ornaments(0);

my $line;
while (1) {
	chomp($line = $term->readline('dbq> '));
	$line =~ s/^\s+//;
	$line =~ s/\s+$//;
	if ($v > 0) {
		printf "read: '%s'\n", $line;
	}
	if ($line =~ /^(exit|quit)$/) {
		last;
	}
	if ($line =~ /^add($|\s.*)$/) {
		add($1);
		next;
	}
	if ($line =~ /^ls($|\s.*)$/) {
		ls($1);
		next;
	}
	if ($line =~ /^on($|\s.*)$/) {
		on($1);
		next;
	}
	if ($line =~ /^off($|\s.*)$/) {
		off($1);
		next;
	}
	if ($line =~ /^rename($|\s.*)$/) {
		dbq_rename($1);
		next;
	}
	if ($line =~ /^del($|\s.*)$/) {
		del($1);
		next;
	}
}

exit(0);

sub add {

	my $q = "INSERT INTO dbq (name, dur, stop) values ";
	$q .=   " (?, ?, ?)";

	my $h = db_prep_alloc('dbq_insert', $q);

	my $name = ask('q name? ');
	if ($name =~ /^\s+$/) {
		print "add needs a non blank name, aborting!";
		return;
	}
	my $str  = ask('q dur? ');
	if ($str =~ /^\s+$/) {
		print "add needs a non blank duration, aborting!";
		return;
	}

	my ($secs,$stop) = durtoss($str);

	if ($v>0) {
		printf "add name/dur/stop = '%s'/%d/'%s'\n", $name, $secs, $stop;
	}

	my $i=1;
	my $rv;
	eval {
		$rv = $h->bind_param($i++, $name, SQL_CHAR);
		$rv = $h->bind_param($i++, $secs, SQL_INTEGER);
		$rv = $h->bind_param($i++, $stop);
	};
	if ($@) {
		printf STDERR "add: bind_param Error: '%s'\n", $@;
	}

	eval {
		$rv = $h->execute();
	};
	if ($@) {
		printf STDERR "add: execute Error: '%s'\n", $@;
	}
}

sub polldb {
	my $q = "SELECT * from dbq where triggered = 0 order by stop";
	my $d;
	my $datenow = Date::Manip::Date->new();
	my $datedb  = Date::Manip::Date->new();
	while (1) {
		my $sth = $db->doquery($q);
		if (!defined($sth) || $sth == -1) {
			return;
		}

		$datenow->parse("now");
		while ($d = $sth->fetchrow_hashref) {
			if ($d->{triggered} > 0) {
				next;
			}
			$datedb->parse($d->{stop});
			my $cret = $datenow->cmp($datedb);
			if ($v>0) {
			printf "%s  %s  %s : %s\n",
				$datenow->printf("%Y-%m-%dT%H:%M:%S"),
				$cret,
				$datedb->printf("%Y-%m-%dT%H:%M:%S"),
				$d->{name};
			}
			if ($cret >= 0) {
				notify($d);
			}
		}
		sleep(5);
	}
	exit(0);
}
# 0 = on
# 1 = off
sub on {
	_onoff(0, 0, @_);
}
sub off {
	_onoff(0, 1, @_);
}

# batch version
sub bon {
	_onoff(1, 0, @_);
}
sub boff {
	_onoff(1, 1, @_);
}

# 

sub _onoff {
	my ($batch, $state, $args) = @_;
	my $sname;
	if ($state == 1) {
		$sname = "off";
	} elsif ($state == 0) {
		$sname = "on";
	} else {
		printf "_onoff: Uh oh, invalid state: '%s'\n", $state;
		return;
	}
	my @ids = ls($args);


	if (!@ids) {
		printf "Uh oh, empty list, ENOTFOUND\n";
		return;
	}

	my $id;
	if ($#ids == 0) {
		my $one = $ids[0]->{id};
		if ($batch == 1) {
			if ($one eq $args) {
				$id = $one;
			} else {
				printf "_onoff: Oops, batch == %d && one = %s && args = %s\n",
					$batch, $one, $args;
				return;
			}
		} else {
			$id = ask('Turn '.$sname.' which id (#, break) ['
			     .$one.'] ? ');
			if ($id =~ /^\s?$/) {
				$id = $one;
			}
		}
	} else {
		if ($batch == 1) {
			printf "_onoff: Oops, batch == %d && \$#ids = %d\n",
				$batch, $#ids;
			return;
		}
		$id = ask('Turn '.$sname.' which id (#, break)? ');
	}
	if ($id eq "break") {
		return;
	}
	my $match;
	foreach my $listid (@ids) {
		if ($listid->{id} == $id) {
			$match = $listid;
			last;
		}
	}
	if (!defined($match)) {
		printf "${sname}: Uh oh, '%s' not in list, ENOTADVISED\n", $id;
		return;
	}

	my ($secs,$stop, $dstr);

	if ($state == 0 && $batch == 0) {

		$dstr = ask('q dur "'.$match->{name}.'" ['.secstohuman($match->{dur}).']? ');
		$dstr =~ s/^\s+//;
		$dstr =~ s/\s+$//;
	}
	my ($q,$h,$i,$rv);
	if ($state == 1) {
		$q = "UPDATE dbq SET triggered = ? ";
		$q .=   "where id = ?";
		$h = db_prep_alloc('update_trigger', $q);
		$i=1;
		eval {
			$rv = $h->bind_param($i++,   1, SQL_INTEGER);
			$rv = $h->bind_param($i++, $id, SQL_INTEGER);
		};
		if ($@) {
			printf STDERR "on: bind_param Error: '%s'\n", $@;
		}
	} elsif (length($dstr) > 0) {
		($secs,$stop) = durtoss($dstr);
		$q = "UPDATE dbq SET triggered = ?, dur = ?, stop = ? ";
		$q .=   "where id = ?";
		$h = db_prep_alloc('update_trigger_dur_stop', $q);
		$i=1;
		eval {
			$rv = $h->bind_param($i++,     0, SQL_INTEGER);
			$rv = $h->bind_param($i++, $secs, SQL_INTEGER);
			$rv = $h->bind_param($i++, $stop);
			$rv = $h->bind_param($i++,   $id, SQL_INTEGER);
		};
		if ($@) {
			printf STDERR "on: bind_param Error: '%s'\n", $@;
		}
	} else {
		($secs,$stop) = durtoss($match->{dur});
		$q = "UPDATE dbq SET triggered = ?, stop = ? ";
		$q .=   "where id = ?";
		$h = db_prep_alloc('update_trigger_stop', $q);
		$i=1;
		eval {
			$rv = $h->bind_param($i++,   0, SQL_INTEGER);
			$rv = $h->bind_param($i++, $stop);
			$rv = $h->bind_param($i++, $id, SQL_INTEGER);
		};
		if ($@) {
			printf STDERR "on: bind_param Error: '%s'\n", $@;
		}
	}
	eval {
		$rv = $h->execute();
	};
	if ($@) {
		printf STDERR "on: execute Error: '%s'\n", $@;
	}
	$h->finish();
}

sub dbq_rename {
	my ($args) = @_;
	my @ids = ls($args);
	my $id;
	if ($#ids == 0) {
		my $one = $ids[0]->{id};
		$id = ask('Rename which id (#, break) ['.$one.'] ? ');
		if ($id =~ /^\s?$/) {
			$id = $one;
		}
	} else {
		$id = ask('Rename which id (#, break)? ');
	}
	if ($id eq "break") {
		return;
	}
	my $q = "SELECT * from dbq where id = ?";
	my $h = db_prep_alloc('dbq_select_id', $q);

	my $i=1;
	my ($rv,$sth);
	eval {
		$rv = $h->bind_param($i++, $id, SQL_INTEGER);
	};
	if ($@) {
		printf STDERR "rename: bind_param Error: '%s'\n", $@;
	}
	eval {
		$rv = $h->execute();
	};
	if ($@) {
		printf STDERR "rename: execute Error: '%s'\n", $@;
	}
	if (!defined($h) || $h == -1) {
		return;
	}
	my $d = $h->fetchrow_hashref;
	$h->finish;
	if (!defined($d)) {
		return;
	}
	my ($secs,$stop);

	my $newname = ask('q rename "'.$d->{name}.'" to? ');
	$newname =~ s/^\s+//;
	$newname =~ s/\s+$//;

	if (length($newname) > 0) {
		my $q = "UPDATE dbq SET name = ? ";
		$q .=   "where id = ?";
		$h = db_prep_alloc('rename', $q);
		$i=1;
		eval {
			$rv = $h->bind_param($i++, $newname, SQL_CHAR);
			$rv = $h->bind_param($i++, $id, SQL_INTEGER);
		};
		if ($@) {
			printf STDERR "rename: bind_param Error: '%s'\n", $@;
		}
	} else {
		printf STDERR "No new name update\n";
		$h->finish();
	}
	eval {
		$rv = $h->execute();
	};
	if ($@) {
		printf STDERR "on: execute Error: '%s'\n", $@;
	}
	$h->finish();
}

sub del {
	my ($args) = @_;
	my @ids = ls($args);

	my $id;
	$id = ask('Delete which id? ');
	my $q = "SELECT * from dbq where id = ?";
	my $h = db_prep_alloc('dbq_select_id', $q);

	my $i=1;
	my $rv;
	eval {
		$rv = $h->bind_param($i++, $id, SQL_INTEGER);
	};
	if ($@) {
		printf STDERR "del: bind_param Error: '%s'\n", $@;
	}
	eval {
		$rv = $h->execute();
	};
	if ($@) {
		printf STDERR "del: execute Error: '%s'\n", $@;
	}
	if (!defined($h) || $h == -1) {
		return;
	}
	my $d = $h->fetchrow_hashref;
	$h->finish;
	if (!defined($d)) {
		return;
	}

	printf "Deleting id %d, %dsecs, %s\n", $d->{id}, $d->{dur}, $d->{name};

	$q = "DELETE from dbq where id = ?";
	$h = db_prep_alloc('dbq_del_id', $q);

	$i=1;
	eval {
		$rv = $h->bind_param($i++, $id, SQL_INTEGER);
	};
	if ($@) {
		printf STDERR "del: bind_param Error: '%s'\n", $@;
	}
	eval {
		$rv = $h->execute();
	};
	if ($@) {
		printf STDERR "del: execute Error: '%s'\n", $@;
	}
}

sub ls {
	my ($args) = @_;
	my $re;
	my $filter = "";
	my $sort = "id";
	my $durtype = "dur"; # dur or left
	my $id;
	my @ids;
	if (defined($args)) {
		$args =~ s/^\s+//;
		if ($v>0) {
			printf "ls with args! '%s'\n", $args;
		}
		if ($args =~ /^\/(.*)\/$/) {
			$re = $1;
		} else {
			foreach my $arg (split(/\s+/, $args)) {
				if ($arg eq "on") {
					$filter .= " triggered = 0";
					next;
				}
				if ($arg eq "off") {
					$filter .= " triggered = 1";
					next;
				}
				if ($arg eq "left") {
					$durtype = "left";
					next;
				}
				if ($arg eq "-t") {
					$sort = "stop";
					next;
				}
				if ($arg =~ /^\/(.*)\/$/) {
					$re = $1;
					next;
				}
				if ($arg =~ /^([0-9]+)$/) {
					$id = $1;
					next;
				}
				printf "ls: unmatched arg '%s'\n", $arg;
			}
			if (defined($id)) {
				if (length($filter) > 0) {
					$filter .= " AND id = ${id}";
				} else {
					$filter .= " id = ${id}";
				}
			}
			if (length($filter) > 0) {
				$filter = " WHERE".$filter;
			}
		
		}
	}
	my $q = "SELECT * from dbq".$filter." order by ".$sort;
	my $sth = $db->doquery($q);
	if (!defined($sth) || $sth == -1) {
		return;
	}
	# header:
	#            '   11. 0 131days 18hours  4mins 27secs 2022-09-04 14:35:30 testing2'
	my $header = "Timer  Expired?      Duration          Stop Time           Description";
	printf "\n%s\n", $header;

	my $d;
	while ($d = $sth->fetchrow_hashref) {
		if (defined($re)) {
			if (! ($d->{name} =~ /${re}/)) {
				next;
			}
		}
		push @ids, $d;

		my $dstr;
		if ($durtype eq "left") {
			my $datenow = Date::Manip::Date->new();
			my $dateend = Date::Manip::Date->new();

			$datenow->parse("now");
			$dateend->parse($d->{stop});
			my $insecs = $dateend->printf("%s") - 
				$datenow->printf("%s");
			$dstr = secstohuman( $insecs );
			if (!defined($dstr)) {
				$dstr = $insecs."secs ENOPARSE";
			}
		} else {
			$dstr = secstohuman( $d->{dur} );
			if (!defined($dstr)) {
				$dstr = $d->{dur}."secs ENOPARSE";
			}
		}


		# '131days 18hours  4mins 27secs' is 29c

		printf "%5d. %d %29s %s %s\n", $d->{id},
			$d->{triggered},
			$dstr, $d->{stop}, $d->{name};
	}
	return @ids;
}

sub ask {
	my ($prompt) = @_;
	my $ret;

	chomp($ret = $term->readline($prompt));
	return $ret;
}
	
sub init_db {
	my ($dbinfo) = @_;

	use DBI::Const::GetInfoType;
	
	$db = FDC::db->new($dbinfo->{dsn}, $dbinfo->{user}, $dbinfo->{pass});

	if (!defined($db)) {
		die "db connection error!";
	}

	my $dbmsname = $db->getdbh()->get_info( $GetInfoType{SQL_DBMS_NAME} );
	my $dbmsver  = $db->getdbh()->get_info( $GetInfoType{SQL_DBMS_VER} );
	my $oidname  = $db->oidname();

	my $dsn = $dbinfo->{dsn};
	if (defined($dsn)) {
		if ($dsn =~ /dbname=([^;]+)/) {
			$dbinfo->{dbname} = $1;
		}
	}
	if (!defined($dbinfo->{dbname})) {
		$dbinfo->{dbname} = "?";
	}

	printf "Connected to dsn '%s' .. dbms name '%s' ver '%s' .. oidname '%s'\n",
		$dbinfo->{dsn}, $dbmsname, $dbmsver, $oidname;

	my @tables;

	my ($serialtype,$blobtype,$tablere,$index_create_re);
	if ($dbmsname eq "PostgreSQL") {
		$serialtype = "serial";
		$blobtype = "bytea";
		$tablere = '\\.%name%$';
		$db->{stats}->{pgsz} = 1;
		$db->{get}->{dbsz} = "SELECT  pg_database_size(datname) db_size FROM pg_database where datname = '";
		$db->{get}->{dbsz} .= $dbinfo->{dbname};
		$db->{get}->{dbsz} .= "' ORDER BY db_size";
		$db->{stats}->{pgct} = $db->do_oneret_query($db->{get}->{dbsz});
		$db->{bbt} = { pg_type => PG_BYTEA };
		$index_create_re = "CREATE %TYPE% INDEX %NAME% ON %TABLE% using btree ( %PARAMS% )";
		$db->do("SET application_name = '?/".getpid()."'");
	} else {
		die "Unhandled dbmsname ${dbmsname}";
	}

	@tables = $db->tables();

	my %tablefound;
	foreach my $tname (@tables) {
		if ($tname =~ /^(information_schema|pg_catalog)\./) {
			next;
		}
		foreach my $tn (('dbq')) {
			my $tre = $tablere;
			$tre =~ s/%name%/$tn/g;
			if ($tname =~ m/$tre/) {
				$tablefound{$tn} = 1;
			}
		}
	}

	if (!defined($tablefound{'dbq'})) {
		my $q = "CREATE TABLE dbq (";
		$q .=   "id ${serialtype}, ";
		$q .=   "name TEXT unique, ";
		$q .=   "dur INT, ";
		$q .=   "stop TIMESTAMP, ";
		$q .=   "triggered INT default 0, ";
		$q .=   "cr  TIMESTAMP default now()";
		$q .=   ")";
		my $sth = $db->doquery($q);
		$sth = init_db_hash($index_create_re, 'dbq_name_idx',
			'dbq', 'name', 'UNIQUE');
		$sth = init_db_hash($index_create_re, 'dbq_id_idx',
			'dbq', 'id');
	}
}

sub db_prep_alloc {
	my ($hname, $prepsql) = @_;

	if (!defined($dbprep->{$hname})) {
		$dbprep->{$hname} = $db->prepare($prepsql);
	}
	return $dbprep->{$hname};
}

sub init_db_hash {
	my ($re, $name, $table, $param, $type) = @_;

	if (!defined($type)) {
		$type = "";
	}
        $re =~ s/%NAME%/$name/;
        $re =~ s/%TABLE%/$table/;
        $re =~ s/%PARAMS%/$param/;
        $re =~ s/%TYPE%/$type/;

	return $db->doquery($re);
}

sub notify {
	my ($d) = @_;

	my $datenow = Date::Manip::Date->new();
	my $delta   = Date::Manip::Delta->new();

	$datenow->parse("now");
	my $deltastr = $d->{dur}."sec";
	$delta->parse($deltastr);
	my $newdate = $datenow->calc($delta, 0);

	tmessage($d, $deltastr, $newdate);
	if (defined($ENV{'DISPLAY'})) {
		xmessage($d, $deltastr, $newdate);
	}
}
sub tmessage {
	my ($d, $deltastr, $newdate) = @_;

	my $id = $d->{id};
	if (defined($memcache->{$id})) {
		if ($d->{stop} eq $memcache->{$id}->{stop}) {
			# we have notified for this before, once
			return;
		}
	}
	$memcache->{$id} = $d;

	printf " %s --> %s : %s\n", $newdate->printf("%Y%m%dT%H:%M:%S"), $deltastr, $d->{name};

	my $wh = $conf->{discord}->{webhook};
	my $notify = $conf->{discord}->{notify};
	if (defined($wh)) {
		my $ats = "";
		if (defined($notify)) {
			foreach my $uid (split(/,/,$notify)) {
				$ats .= " <\@${uid}>";
			}
		}
		my $str = sprintf("dwebhook %s '%s' '%s%s'", $wh, 'dbq',
			$d->{name}, $ats);
		system($str);
	}
}

sub xmessage {
	my ($d, $deltastr, $newdate) = @_;

	# XXX check for shell escapes until perl native notify
	my $cmd = "xmessage";
	$cmd .= " -timeout 5";
	$cmd .= " -center";
	$cmd .= " -print";
	$cmd .= " -buttons Snooze:0,Dismiss:1,Restart:2";
	$cmd .= " -default Snooze ";
	$cmd .= $d->{name}.", time: $deltastr";
	open(XM,"$cmd 2> /dev/null |");
	my $line;
	while(<XM>) {
		chomp($line = $_);
		printf "returned '%s' for %s\n", $line, $d->{name};
		if ($line eq "Dismiss") {
			#dismiss($d);
			boff($d->{id});
			last;
		}
		if ($line eq "Restart") {
			printf "Restart id %d new end %s\n",
				$d->{id},
				$newdate->printf("%Y-%m-%dT%H:%M:%S");
		}
	}
	close(XM);
}

sub dismiss {
	my ($d) = @_;

	my $h = update_trigger_q();
	
	my $i=1;
	my $rv;
	eval {
		$rv = $h->bind_param($i++, 1, SQL_INTEGER);
		$rv = $h->bind_param($i++, $d->{id}, SQL_INTEGER);
	};
	if ($@) {
		printf STDERR "dismiss: bind_param Error: '%s'\n", $@;
	}
	eval {
		$rv = $h->execute();
	};
	if ($@) {
		printf STDERR "dismiss: execute Error: '%s'\n", $@;
	}
}

sub update_trigger_q {
	my $q = "UPDATE dbq SET triggered = ? where id = ?";
	return db_prep_alloc('dbq_update_trigger', $q);
}
	

sub secstohuman {
		my ($duration) = @_;

		my ($day,$hour,$min,$sec) = secstodhms( $duration );

		my $dstr = "";
		if ($day > 0) {
			$dstr .= sprintf " %2ddays", $day;
		} elsif ($day == 1) {
			$dstr .= sprintf " %2dday", $day;
		}
		if ($hour > 1) {
			$dstr .= sprintf " %2dhours", $hour;
		} elsif ($hour == 1) {
			$dstr .= sprintf " %2dhour", $hour;
		}
		if ($min > 1) {
			$dstr .= sprintf " %2dmins", $min;
		} elsif ($min == 1) {
			$dstr .= sprintf " %2dmin", $min;
		}
		if ($sec > 1) {
			$dstr .= sprintf " %2dsecs", $sec;
		} elsif ( $sec == 1) {
			$dstr .= sprintf " %2dsec", $sec;
		} elsif ($sec < 1 && length($dstr) < 1) {
			$dstr .= sprintf " %2dsecs", $sec;
		}

		$dstr =~ s/^\s+//;
		$dstr =~ s/\s+$//;

		return $dstr;
}

sub secstodhms {
	my ($secs) = @_;

	my ($d, $h,$m,$s);
	$d = POSIX::floor( $secs / (24*60*60) );
	$h = POSIX::floor( ( $secs - $d * (24*60*60) ) / 3600 );
	$m = POSIX::floor( ( $secs - $d * (24*60*60) - $h * (60*60) ) / 60 );
	$s = $secs - $d * (24*60*60) - $h * (60*60) - $m * 60;

	return ($d, $h, $m, $s);
}

# Duration (from now) to Seconds and Stop Time
sub durtoss {
	my ($str) = @_;

	my $date = Date::Manip::Date->new();
	my $delta = Date::Manip::Delta->new();

	$date->parse("now");
	$delta->parse('+'.$str);
	my $newdate = $date->calc($delta, 0); # add = 0, subtract = 1
	my $stop = $newdate->printf("%Y-%m-%dT%H:%M:%S");
	my $dur = $newdate->printf("%s") - $date->printf("%s");

	return ($dur,$stop);
}
